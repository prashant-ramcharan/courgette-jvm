group 'io.github.prashant-ramcharan'
version '6.19.0'

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'maven'
apply plugin: 'signing'

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

ext {
    cucumberVersion = '7.23.0'
    testNGVersion = '7.9.0'
    jacksonVersion = '2.17.0'
    httpcomponentsVersion = '4.5.13'
    extentreportVersion = '5.0.3'
    mustacheVersion = '0.9.6'
    gsonVersion = '2.11.0'
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    compile "io.cucumber:cucumber-core:$cucumberVersion"
    compile "io.cucumber:cucumber-java:$cucumberVersion"
    compile "io.cucumber:cucumber-java8:$cucumberVersion"
    compile "io.cucumber:cucumber-junit:$cucumberVersion"
    compile("io.cucumber:cucumber-testng:$cucumberVersion") {
        exclude group: 'com.google.guava'
        exclude group: 'org.testng'
    }
    compile "org.testng:testng:$testNGVersion"
    compile "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:$jacksonVersion"
    compile "com.fasterxml.jackson.module:jackson-module-parameter-names:$jacksonVersion"
    compile "org.apache.httpcomponents:httpclient:$httpcomponentsVersion"
    compile "org.apache.httpcomponents:httpmime:$httpcomponentsVersion"
    compile "com.aventstack:extentreports:$extentreportVersion"
    compile "com.github.spullara.mustache.java:compiler:$mustacheVersion"
    compile "com.google.code.gson:gson:$gsonVersion"
}

task buildJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives buildJar, javadocJar, sourcesJar
}

jar {
    manifest {
        attributes 'Automatic-Module-Name': 'prashant.ramcharan.courgette.jvm'
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact buildJar
        }
    }
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://ossrh-staging-api.central.sonatype.com/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://central.sonatype.com/repository/maven-snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Courgette JVM'
                packaging 'jar'
                description 'Multi-threaded | Parallel Cucumber-JVM | Parallelize your Java Cucumber tests on a feature level or on a scenario level.'
                url 'https://github.com/prashant-ramcharan/courgette-jvm'

                scm {
                    url 'https://github.com/prashant-ramcharan/courgette-jvm'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id 'prashant-ramcharan'
                        name 'Prashant Ramcharan'
                        email 'info@prashant-ramcharan.co.uk'
                    }
                }
            }
        }
    }
}
